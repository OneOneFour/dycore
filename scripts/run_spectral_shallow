#!/bin/csh -f
# Minimal runscript: Spectral shallow model
set echo
#--------------------------------------------------------------------------------------------------------
# define variables
set platform  = sgi                                      # A unique identifier for your platform
set npes      = 1                                        # number of processors
set workdir   = $cwd:h/workdir                           # where model is run and model output is produced
set execdir   = $cwd:h/exec_spectral_shallow.$platform   # where code is compiled and executable is created
set template  = $cwd:h/bin/mkmf.template.$platform       # path to template for your platform
set mkmf      = $cwd:h/bin/mkmf                          # path to executable mkmf
set sourcedir = $cwd:h/src                               # path to directory containing model source code
set pathnames = $cwd:h/input/spectral_shallow_pathnames  # path to file containing list of source paths
set namelist  = $cwd:h/input/spectral_shallow_namelist   # path to namelist file
set diagtable = $cwd:h/input/spectral_shallow_diag_table # path to diagnositics table
set mppnccombine = $cwd:h/bin/mppnccombine.$platform     # path to executable mppnccombine
#--------------------------------------------------------------------------------------------------------
# compile mppnccombine.c, needed only if $npes > 1
if ( ! -f $mppnccombine ) then
  cc -O -o $mppnccombine -I/usr/local/include -L/usr/local/lib $cwd:h/postprocessing/mppnccombine.c -lnetcdf
endif
#--------------------------------------------------------------------------------------------------------
# setup directory structure
if ( ! -d $execdir ) mkdir $execdir
if ( -e $workdir ) then
  echo "ERROR: Existing workdir may contaminate run.  Move or remove $workdir and try again."
  exit 1
endif
mkdir $workdir $workdir/INPUT $workdir/RESTART
#--------------------------------------------------------------------------------------------------------
# compile the model code and create executable
cd $execdir
$mkmf -p fms.x -t $template -c "-Duse_libMPI -Duse_netCDF" -a $sourcedir $pathnames /usr/local/include
make -f Makefile
cd $workdir
#--------------------------------------------------------------------------------------------------------
# set run length and time step, get input data and executable
cat > input.nml <<EOF
 &main_nml
     days   = 2,
     dt_atmos = 1200 /
EOF
cat $namelist >> input.nml
cp $diagtable diag_table
cp $execdir/fms.x fms.x
#--------------------------------------------------------------------------------------------------------
# run the model with mpirun
mpirun -np $npes fms.x
#--------------------------------------------------------------------------------------------------------
# combine netcdf files
if ( $npes > 1 ) then
  foreach ncfile (`/bin/ls *.nc.0000`)
    $mppnccombine $ncfile:r
    if ($status == 0) rm -f $ncfile:r.????
  end
endif
