#!/bin/csh -f

source /etc/profile.d/modules.csh
module add pgi
module add mpich
module add netcdf

# Minimal runscript: Spectral atmospheric core with Held-Suarez forcing
set echo 
#--------------------------------------------------------------------------------------------------------
# define variables
set platform  = pgi                                  # A unique identifier for your platform
set npes      = 1                                    # number of processors
set workdir   = $cwd:h/workdir                       # where model is run and model output is produced
set execdir   = $cwd:h/exec_spectral.$platform       # where code is compiled and executable is created
set template  = $cwd:h/bin/mkmf.template.$platform   # path to template for your platform
set mkmf      = $cwd:h/bin/mkmf                      # path to executable mkmf
set sourcedir = $cwd:h/src                           # path to directory containing model source code
set pathnames = $cwd:h/input/spectral_pathnames      # path to file containing list of source paths
set namelist  = $cwd:h/input/spectral_namelist       # path to namelist file
set diagtable = $cwd:h/input/hs_diag_table           # path to diagnositics table
set fieldtable = $cwd:h/input/spectral_field_table   # path to field table (specifies tracers)
set mppnccombine = $cwd:h/bin/mppnccombine.$platform # path to executable mppnccombine
#--------------------------------------------------------------------------------------------------------
# compile mppnccombine.c, needed only if $npes > 1
if ( ! -f $mppnccombine ) then
  pgcc -O -o $mppnccombine -I$INC_NETCDF -L$LIB_NETCDF $cwd:h/ddaniela/atm_dycores/atm_dycores_test/postprocessing/mppnccombine.c -lnetcdf
endif
#--------------------------------------------------------------------------------------------------------
# setup directory structure
if ( ! -d $execdir ) mkdir $execdir
if ( -e $workdir ) then
  echo "ERROR: Existing workdir may contaminate run.  Move or remove $workdir and try again."
  exit 1
endif
mkdir $workdir $workdir/INPUT $workdir/RESTART
#--------------------------------------------------------------------------------------------------------
# compile the model code and create executable
cd $execdir
$mkmf -p fms.x -t $template -c "-Duse_libMPI -Duse_netCDF" -a $sourcedir $pathnames 
make -f Makefile
cd $workdir
#--------------------------------------------------------------------------------------------------------
# set run length and time step, get input data and executable
cat > input.nml <<EOF
 &main_nml
     days   = 1,
     dt_atmos = 1200 /
EOF
cat $namelist >> input.nml
cp $diagtable diag_table
cp $fieldtable field_table
cp $execdir/fms.x fms.x
#--------------------------------------------------------------------------------------------------------
# run the model with mpirun
echo mpirun -np $npes $cwd:h/fms.x
#--------------------------------------------------------------------------------------------------------
# combine netcdf files
if ( $npes > 1 ) then
  foreach ncfile (`/bin/ls *.nc.0000`)
    $mppnccombine $ncfile:r
    if ($status == 0) rm -f $ncfile:r.????
  end
endif
